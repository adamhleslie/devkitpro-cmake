include_guard(GLOBAL)

macro(dkp_message level msg)
    if(NOT devkitpro_FIND_QUIETLY OR ${level} STREQUAL "FATAL_ERROR")
        if(${level} STREQUAL "CHECK_PASS" OR ${level} STREQUAL "CHECK_FAIL")
            message(${level} "${msg}")
        else()
            message(${level} "[devkitPro] ${msg}")
        endif()
    endif()
endmacro()

# Custom find function for a component's file found in a devkitPro package
# CMake variable or environment variable may be used to bypass default path
# Sets the devkitpro_${component}_FOUND / devkitpro_${component}_NOT_FOUND_MESSAGE variables
function(dkp_find_component_file component variable file_path_relative package)
    cmake_path(GET file_path_relative STEM file_stem)
    dkp_message(CHECK_START "Finding ${file_stem} (${variable})")
    set(found FALSE)
    if(DEFINED ${variable})
        if(EXISTS ${${variable}})
            dkp_message(CHECK_PASS "Using location from CMake variable: ${${variable}}")
            set(found TRUE)
        else()
            dkp_message(CHECK_FAIL "Invalid location from CMake variable: ${${variable}}")
        endif()
    elseif(DEFINED ENV{${variable}})
        if(EXISTS $ENV{${variable}})
            set(${variable} $ENV{${variable}} PARENT_SCOPE)
            dkp_message(CHECK_PASS "Using location from environment variable: $ENV{${variable}}")
            set(found TRUE)
        else()
            dkp_message(CHECK_FAIL "Invalid location from environment variable: $ENV{${variable}}")
        endif()
    else()
        if(NOT DEVKITPRO)
            dkp_message(CHECK_FAIL "DEVKITPRO must be defined")
        endif()
        cmake_path(ABSOLUTE_PATH file_path_relative BASE_DIRECTORY ${DEVKITPRO} OUTPUT_VARIABLE file_path_absolute)
        if(EXISTS ${file_path_absolute})
            set(${variable} ${file_path_absolute} PARENT_SCOPE)
            dkp_message(CHECK_PASS "Found ${file_path_absolute}")
            set(found TRUE)
        else()
            dkp_message(CHECK_FAIL "Not at expected location ${file_path_absolute}")
        endif()
    endif()

    if(found)
        set(devkitpro_${component}_FOUND TRUE PARENT_SCOPE)
    else()
        set(devkitpro_${component}_FOUND FALSE PARENT_SCOPE)
        set(devkitpro_${component}_NOT_FOUND_MESSAGE "${file_stem} not found. This file is part of the package '${package}'. Please check your installation, or install via:\n    dkp-pacman -S ${package}" PARENT_SCOPE)
    endif()
endfunction()

function(dkp_get_relative_and_absolute path base_directory relative_path_variable absolute_path_variable)
    cmake_path(IS_ABSOLUTE path path_is_absolute)
    if(${path_is_absolute})
        cmake_path(IS_PREFIX base_directory ${path} is_prefix)
        if(NOT ${is_prefix})
            dkp_message(FATAL_ERROR "Base directory \"${base_directory}\" is not a prefix for path \"${path}\"")
        endif()

        cmake_path(SET absolute_path ${path})
        cmake_path(RELATIVE_PATH path BASE_DIRECTORY ${base_directory} OUTPUT_VARIABLE relative_path)
    else()
        cmake_path(SET relative_path ${path})
        cmake_path(ABSOLUTE_PATH relative_path BASE_DIRECTORY ${base_directory} OUTPUT_VARIABLE absolute_path)
    endif()

    set(${absolute_path_variable} ${absolute_path} PARENT_SCOPE)
    set(${relative_path_variable} ${relative_path} PARENT_SCOPE)
endfunction()

function(dkp_make_absolute_if_relative path_variable default_base_directory)
    cmake_path(IS_ABSOLUTE ${path_variable} is_absolute)
    if(${is_absolute})
        cmake_path(SET absolute_path ${${path_variable}})
    else()
        cmake_path(ABSOLUTE_PATH ${path_variable} BASE_DIRECTORY ${default_base_directory} OUTPUT_VARIABLE absolute_path)
    endif()

    set(${path_variable} ${absolute_path} PARENT_SCOPE)
endfunction()
